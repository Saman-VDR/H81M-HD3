# Maintained by: uglyJoe for: github.com/Saman-VDR/H81M-HD3
# File: iMac142-SSDT-3.txt
#
# Transform into iMac14,2
# SaSsdt patch for IGP + GFX0
#

# Remove devices P0PA and P0PB
into device name_adr 0x00010001 remove_entry;
into device name_adr 0x00010002 remove_entry;

# Rename GFX0 to IGPU
into_all device name_adr 0x00020000 set_label begin IGPU end; 
into_all all code_regex GFX0 replaceall_matched begin IGPU end;

# Add method _DSM into IGPU
into method label _DSM parent_label IGPU remove_entry;
into device label IGPU insert
begin
Method (_DSM, 4, NotSerialized)\n
{\n
   If (LEqual (Arg2, Zero)) { Return (Buffer(One) { 0x03 } ) }\n
   Return (Package (0x04)\n
   {\n
          "AAPL,ig-platform-id",  Buffer (0x04) { 0x03, 0x00, 0x22, 0x0D },\n
          "hda-gfx", Buffer (0x0A) { "onboard-1" }\n
   })\n
}\n
end;

# Rename B0D3 to HDAU
into_all device name_adr 0x00030000 set_label begin HDAU end; 
into_all all code_regex B0D3 replaceall_matched begin HDAU end;

# Add method _DSM into HDAU
into method label _DSM parent_label HDAU remove_entry;
into device label HDAU insert
begin
Method (_DSM, 4, NotSerialized)\n
{\n
   If (LEqual (Arg2, Zero)) { Return (Buffer(One) { 0x03 } ) }\n
   Return (Package(0x04)\n
   {\n
          "layout-id", Buffer(0x04) { 0x01, 0x00, 0x00, 0x00 },\n
          "hda-gfx", Buffer(0x0A) { "onboard-1" }\n
   })\n
}\n
end;

# Rename PEGP to GFX0
into_all device label PEGP set_label begin GFX0 end;

# Add method _DSM into GFX0
into method label _DSM parent_label GFX0 remove_entry;
into device label GFX0 insert
begin
Method (_DSM, 4, NotSerialized)\n
{\n
   If (LEqual (Arg2, Zero)) { Return (Buffer(One) { 0x03 } ) }\n
   Return (Package (0x0A)\n
   {\n
        "@0,connector-type", \n
        Buffer (0x04) { 0x00, 0x08, 0x00, 0x00 }, \n
        "@1,connector-type", \n
        Buffer (0x04) { 0x00, 0x08, 0x00, 0x00 }, \n
        "@2,connector-type", \n
        Buffer (0x04) { 0x00, 0x08, 0x00, 0x00 }, \n
        "@3,connector-type", \n
        Buffer (0x04) { 0x00, 0x08, 0x00, 0x00 }, \n
        "hda-gfx", \n
        Buffer (0x0A) { "onboard-2" } \n
   })\n
}\n
end;

# Add device HDAU
into device label HDAU parent_label PEG0 remove_entry;
into device label PEG0 insert
begin
Device (HDAU)\n
{\n
    Name (_ADR, One) \n
    Method (_DSM, 4, NotSerialized)\n
    {\n
       If (LEqual (Arg2, Zero)) { Return (Buffer(One) { 0x03 } ) }\n
       Return (Package(0x02)\n
       {\n
          "hda-gfx", Buffer(0x0A) { "onboard-2" }\n
       })\n
    }\n
}\n
end;
