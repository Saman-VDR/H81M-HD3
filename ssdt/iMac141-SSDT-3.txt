# Maintained by: uglyJoe for: github.com/Saman-VDR/H81M-HD3
# File: iMac141-SSDT-3.txt
#
# Transform into iMac14,1
# SaSsdt patch for
# Integrated Graphics only
#

# Remove devices PEGP, P0PA and P0PB
into device name_adr 0x00010000 remove_entry;
into device name_adr 0x00010001 remove_entry;
into device name_adr 0x00010002 remove_entry;

# Rename GFX0 to IGPU
into_all device name_adr 0x00020000 set_label begin IGPU end; 
into_all all code_regex GFX0 replaceall_matched begin IGPU end;

# Add method _DSM into IGPU
into method label _DSM parent_label IGPU remove_entry;
into device label IGPU insert
begin
Method (_DSM, 4, NotSerialized)\n
{\n
   If (LEqual (Arg2, Zero)) { Return (Buffer(One) { 0x03 } ) }\n
   Return (Package ()\n
   {\n
       "AAPL,ig-platform-id",  Buffer (0x04) { 0x03, 0x00, 0x22, 0x0D }, \n
       "device-id", Buffer() { 0x12, 0x04, 0x00, 0x00 }, \n
       "model", Buffer() { "Intel HD Graphics 4600" }, \n
       "hda-gfx", Buffer (0x0A) { "onboard-1" }, \n
       "framebuffer-patch-enable", Buffer(0x04) { 0x01, 0x00, 0x00, 0x00 }, \n
       "framebuffer-con0-type",    Buffer(0x04) { 0x00, 0x08, 0x00, 0x00 }, \n
       "framebuffer-con0-enable",  Buffer(0x04) { 0x01, 0x00, 0x00, 0x00 }, \n
       "framebuffer-con1-type",    Buffer(0x04) { 0x00, 0x08, 0x00, 0x00 }, \n
       "framebuffer-con1-enable",  Buffer(0x04) { 0x01, 0x00, 0x00, 0x00 }  \n
   })\n
}\n
end;

# Rename B0D3 to HDAU
into_all device name_adr 0x00030000 set_label begin HDAU end; 
into_all all code_regex B0D3 replaceall_matched begin HDAU end;

# Add method _DSM into HDAU
into method label _DSM parent_label HDAU remove_entry;
into device label HDAU insert
begin
Method (_DSM, 4, NotSerialized)\n
{\n
   If (LEqual (Arg2, Zero)) { Return (Buffer(One) { 0x03 } ) }\n
   Return (Package(0x0C)\n
   {\n
       "hda-gfx", \n
       Buffer (0x0A) \n
       { \n
           "onboard-1" \n
       }, \n
       "AAPL,slot-name", \n
       "Built In", \n
       "name", \n
       "HDMI Controller", \n
       "built-in", \n
       Buffer (One) \n
       { \n
           0x00 \n
       }, \n
       "model", \n
       Buffer (0x1C) \n
       { \n
           "Intel HDMI Audio Controller" \n
       }, \n
       "device_type", \n
       Buffer (0x16) \n
       { \n
           "Multimedia Controller" \n
       } \n
   })\n
}\n
end;
